plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}


archivesBaseName = 'core'
group 'io.github.bytes-chaser'
version '1.0.0'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.2'
}

test {
    useJUnitPlatform()
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    from javadoc.destinationDir
    archiveClassifier = "javadoc"
}

task sourcesJar(type: Jar, dependsOn: classes) {
    from sourceSets.main.allSource
    archiveClassifier = "source"
}

artifacts {
    archives javadocJar
    archives sourcesJar
}

publishing {
    publications {
        mavenJava(MavenPublication) {

            artifactId = 'core'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }

            pom {
                name = 'eazy-ssh'
                description = 'Highly-customizable Framework for simplifying SSH interactions.'
                url = 'https://github.com/bytes-chaser/eazy-ssh-core'
                inceptionYear = '2022'

                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        name = 'bytes-chaser'
                        email = 'eazy.ssh@gmail.com'
                    }
                }
                scm {
                    connection = 'scm:git:git://github.com/bytes-chaser/eazy-ssh-core.git'
                    developerConnection = 'scm:git:ssh://github.com:/bytes-chaser/eazy-ssh-core'
                    url = 'https://github.com/bytes-chaser/eazy-ssh-core'
                }
            }
        }
    }

    repositories {
        maven {
            name = "OSSRH"
            url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}